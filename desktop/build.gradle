dependencies {
    compile project(":backend-impl")
    compile project(":frontend-impl")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
}

task createProperties(dependsOn: processResources) {
    doLast {
        file("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['libgdx_version']=project.gdxVersion.toString()
            p.store w, null
        }
    }
}
classes.dependsOn createProperties

setArchivesBaseName("ribz-desktop")

project.ext.mainClassName = "fr.foacs.ribz.desktop.DesktopLauncher"

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = sourceSets.main.output.resourcesDir
    setIgnoreExitValue(true)
}

task dist(type: Jar) {
    from {configurations.compile.collect {zipTree(it)}}
    with jar
    manifest {
        attributes ('Main-Class': project.mainClassName, 'SplashScreen-Image': 'foacs.png')
    }

}

dist.dependsOn classes

sonarqube.properties{
    property "sonar.coverage.jacoco.xmlReportPaths", ""
}