plugins {
    id 'org.sonarqube' version "3.0"
    id 'base'
}

allprojects {
    apply plugin: 'jacoco'

    group 'fr.foacs'
    version '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    ext {
        appName = "ribz"
        gdxVersion = '1.9.10'
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 11

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
        testCompile "org.mockito:mockito-core:2.+"
        testCompile "org.mockito:mockito-junit-jupiter:2.+"

        testImplementation 'org.awaitility:awaitility:4.0.3'

        /* LOMBOK */
        compile 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        /* LOMBOK Test */
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

        /* SLF4J */
        implementation "org.slf4j:slf4j-api:1.7.30"
        /* SLF4J-BINDING */
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3"
        /* LOG4J2 */
        implementation "org.apache.logging.log4j:log4j-api:2.13.3"
        implementation "org.apache.logging.log4j:log4j-core:2.13.3"

        /*Google guava*/
        implementation 'com.google.guava:guava:28.2-jre'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    }

    sonarqube {
        properties {
            property "sonar.sources", "src/main/java"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    test {
        useJUnitPlatform()
        reports {
            junitXml.enabled = true
            html.enabled = false
        }
    }
    check.dependsOn jacocoTestReport
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.organization", "foacs"
        property "sonar.projectKey", "Foacs_ribz"
    }
}

/*
 * Generate doc under 'doc/latest'.
 */
task ribzdoc(type: Javadoc) {
    title "RIBZ API"

    subprojects.each {
        source it.sourceSets.main.allJava
        classpath += it.configurations.compile
    }

    destinationDir =  file("${buildDir}/doc/latest")
}

/*
 * Generate doc under 'doc/version/{version_number}' with version number in title.
 */
task ribzdocVersion(type: Javadoc) {
    title "RIBZ API version ${version}"

    subprojects.each {
        source it.sourceSets.main.allJava
        classpath += it.configurations.compile
    }

    destinationDir =  file("${buildDir}/doc/version/${version}/")
}
